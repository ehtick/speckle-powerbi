// function for federating multiple tables by combining them and creating a concatenated Version Object ID
(tables as list, optional excludeData as logical) as table =>
let
    ViewerOnly = if excludeData = null then false else excludeData,
    
    // filter columns from each table if excludeData is true
    ProcessedTables = List.Transform(
        tables,
        each
            if ViewerOnly then 
                Table.SelectColumns(_, {"Version Object ID", "Object IDs"}, MissingField.Ignore)
            else 
                _
    ),
    
    CombinedTable = Table.Combine(ProcessedTables),
    
    DistinctVersionObjectIDs = List.Distinct(CombinedTable[Version Object ID]),
    ConcatenatedVersionObjectIDs = Text.Combine(DistinctVersionObjectIDs, ","),
    
    // Replace all Version Object ID values with the concatenated string
    FederatedTable = Table.ReplaceValue(
        CombinedTable, 
        each [Version Object ID], 
        ConcatenatedVersionObjectIDs, 
        Replacer.ReplaceText, 
        {"Version Object ID"}
    )
in
    FederatedTable